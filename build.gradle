plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'jacoco'
	id 'net.ltgt.apt' version '0.21'
}
apply plugin: 'net.ltgt.apt-idea'
apply plugin: 'net.ltgt.apt-eclipse'
group = 'com.conta.cloud.sat'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation('org.springframework.security.oauth:spring-security-oauth2')
	compile 'org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.1.3.RELEASE'
	compileOnly 'org.projectlombok:lombok'
	compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'    
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly "org.projectlombok:lombok"

	// mapstruct	
	implementation "org.mapstruct:mapstruct:1.3.1.Final"
    annotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

    testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

	// unlike testCompile, these configurations do not inherit from their compile counterparts
	// so if your tests depend on lombok, make sure you have specified these:
	testAnnotationProcessor "org.projectlombok:lombok"
	testCompileOnly "org.projectlombok:lombok"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}

def jacocoExclude = ['com/conta/cloud/sat/SatCatalogApplication**',
					'com/conta/cloud/sat/config/**',
					'exception/**',
					'com/conta/cloud/sat/rest/exception/**',
					'com/conta/cloud/sat/mappers/**']

jacoco {
	toolVersion = '0.8.5'
	reportsDir = file('$buildDir/reports/jacoco')
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacoco")
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: jacocoExclude
			)
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.8
			}
		}

		rule {
			enabled = false
			element = 'CLASS'
			includes = ['com.conta.cloud.sat.*']

			limit {
				counter = 'LINE'
				value = 'TOTALCOUNT'
				maximum = 0.9
			}
		}
	}
}